# Salt Minion floscript

house minion

# init name in .raet.udp.stack.local from value in .salt.etc.id
init role in .raet.udp.stack.local from value in .salt.etc.id

init .salt.uxd.stack.local to name "minion" lanename "minion"

framer minionudpstack be active first setup
    # Begin the pre-flight checks
    frame setup
        enter
            do salt raet cleanup
            do salt raet road stack setup per inode ".raet.udp.stack"
            do salt manor lane setup
        go loadmodules

    # Load the minion mods
    frame loadmodules
        do load modules at enter
        go start

    # OK, let's start the minion up
    frame start
        # Start the inbound framer
        bid start inbound
        # Start the bootstrap framer
        bid start bootstrap
        # Start the eventing framer
        bid start eventing
        # Start the functionmanage framer
        bid start functionmanager
        # Start the manage framer
        bid start manager
        # Start the outbound framer
        bid start outbound

        # Cleanup on exit
        exit
            do salt raet road stack closer per inode ".raet.udp.stack."
            do salt raet lane stack closer per inode ".salt.uxd.stack."

# Framer for handling inbound traffic
framer inbound be inactive first start
    frame start
        do salt raet road stack service rx

framer bootstrap be inactive first join
    frame join
        print Joining...
        enter
            do salt raet road stack joiner per inode ".raet.udp.stack."
        recur
            do salt raet road stack joined per inode ".raet.udp.stack."
            do salt raet road stack rejected per inode ".raet.udp.stack."

        go next if joined in .raet.udp.stack.status
        go abort if rejected in .raet.udp.stack.status
        #go abort if elapsed >= 10

    frame joined
        print Joined
        go next

    frame allow
        print Allowing...
        enter
            do salt raet road stack allower per inode ".raet.udp.stack."
        recur
            do salt raet road stack allowed per inode ".raet.udp.stack."

        go next if allowed in .raet.udp.stack.status
        #go abort if elapsed >= 5

    frame allowed
        print Allowed
        go next

    frame pillar
        enter
            do load pillar
        go loading

    frame loading
        enter
            do load modules
        go router

    frame router
        bid start manager #start alive presence from minion side
        do router
        go pillar if .salt.var.pillar_refresh
        go loading if .salt.var.module_refresh

    frame abort
        bid stop all

framer eventing be inactive first event
    frame event
        do eventer

framer functionmanager be inactive first checkexec
    frame checkexec
        do nix executor

framer manager be inactive first start at 10.0
    frame start
         do salt raet road stack manager per inode ".raet.udp.stack"

# Framer for handling outbound traffic
framer outbound be inactive first start
    frame start
        do salt raet road stack service tx

#framer scheduler be inactive first start
#    frame start
#        do schedule
